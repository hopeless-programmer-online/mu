file  ::= SPC? stats

stats ::= (stat SPC?)*
stat  ::= if | ret | expr

    if    ::= "if" SPC? expr SPC? ":" SPC? stat
    ret   ::= "return" SPC? expr

prog  ::= "program" SPC? args SPC? expr
    arg   ::= name
    args  ::= "(" SPC? ( arg SPC? ("," SPC? arg SPC?)* )? ")"

expr  ::= ass | call | prog | block | term
    block ::= "{" SPC? (stat SPC?)* "}"
    ass   ::= name SPC? ("," SPC? name SPC?)* "=" SPC? expr
    call  ::= term "(" SPC? (expr SPC? ("," SPC? expr SPC?)* )? ")"

term  ::= name | LIT | group
    group ::= "(" SPC? expr SPC? ")"
    name  ::= WORD
    LIT   ::= int
    int   ::= [0-9](SPC? [0-9])*

WORD  ::= [a-zA-Z_] [a-zA-Z0-9_]*
SPC   ::= [ \n\r]+
